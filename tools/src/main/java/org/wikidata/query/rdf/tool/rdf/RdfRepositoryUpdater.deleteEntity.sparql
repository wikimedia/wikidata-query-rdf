# clear all statements (except shared) about an entity
DELETE {
  ?entityStatements ?statementPredicate ?statementObject .
}
WHERE {
  VALUES ?entity {
     %entityList%
  }
  ?entity ?entityToStatementPredicate ?entityStatements .
  FILTER( STRSTARTS(STR(?entityStatements), "%uris.statement%") ) .
  ?entityStatements ?statementPredicate ?statementObject .
};

DELETE {
    ?siteLink ?sitelinkPredicate ?sitelinkObject
}
WHERE {
  VALUES ?entity {
     %entityList%
  }
  ?siteLink %schema:about% ?entity .
  ?siteLink ?sitelinkPredicate ?sitelinkObject .
};

# Clear all statements (except shared values or references) about a sense or a form a lexeme
DELETE {
  ?formOrSenseStatements ?statementPredicate ?statementObject .
}
WHERE {
  VALUES ?entity {
     %entityList%
  }
  VALUES ?formOrSensePredicate {%ontolex:lexicalForm% %ontolex:sensePredicate%}
  ?entity ?formOrSensePredicate ?formOrSense .
  ?formOrSense ?formOrSenseToStatementPredicate ?formOrSenseStatements .
  ?formOrSenseStatements ?statementPredicate ?statementObject .
  FILTER (STRSTARTS(STR(?formOrSenseStatements), "%uris.statement%"))
};
# Clear all senses or forms
DELETE {
  ?formOrSense ?formOrSensePred ?formOrSenseObject .
}
WHERE {
  VALUES ?entity {
     %entityList%
  }
  VALUES ?formOrSensePredicate {%ontolex:lexicalForm% %ontolex:sensePredicate%}
  ?entity ?formOrSensePredicate ?formOrSense .
  ?formOrSense ?formOrSensePred ?formOrSenseObject .
};

DELETE {
  ?entity ?entityPredicate ?entityObject .
}
WHERE {
  VALUES ?entity {
      %entityList%
  }
  ?entity ?entityPredicate ?entityObject .
};