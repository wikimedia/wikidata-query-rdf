# clear all statements (except shared) about an entity
DELETE {
  ?entityStatements ?statementPredicate ?statementObject .
}
WHERE {
  VALUES ?entity {
     <http://acme.test/entity/entity123>
  }
  ?entity ?entityToStatementPredicate ?entityStatements .
  FILTER( STRSTARTS(STR(?entityStatements), "http://acme.test/entity/statement/") ) .
  ?entityStatements ?statementPredicate ?statementObject .
};

DELETE {
    ?siteLink ?sitelinkPredicate ?sitelinkObject
}
WHERE {
  VALUES ?entity {
     <http://acme.test/entity/entity123>
  }
  ?siteLink <http://schema.org/about> ?entity .
  ?siteLink ?sitelinkPredicate ?sitelinkObject .
};

# Clear all statements (except shared values or references) about a sense or a form a lexeme
DELETE {
  ?formOrSenseStatements ?statementPredicate ?statementObject .
}
WHERE {
  VALUES ?entity {
     <http://acme.test/entity/entity123>
  }
  VALUES ?formOrSensePredicate {<http://www.w3.org/ns/lemon/ontolex#lexicalForm> <http://www.w3.org/ns/lemon/ontolex#sense>}
  ?entity ?formOrSensePredicate ?formOrSense .
  ?formOrSense ?formOrSenseToStatementPredicate ?formOrSenseStatements .
  ?formOrSenseStatements ?statementPredicate ?statementObject .
  FILTER (STRSTARTS(STR(?formOrSenseStatements), "http://acme.test/entity/statement/"))
};
# Clear all senses or forms
DELETE {
  ?formOrSense ?formOrSensePred ?formOrSenseObject .
}
WHERE {
  VALUES ?entity {
     <http://acme.test/entity/entity123>
  }
  VALUES ?formOrSensePredicate {<http://www.w3.org/ns/lemon/ontolex#lexicalForm> <http://www.w3.org/ns/lemon/ontolex#sense>}
  ?entity ?formOrSensePredicate ?formOrSense .
  ?formOrSense ?formOrSensePred ?formOrSenseObject .
};

DELETE {
  ?entity ?entityPredicate ?entityObject .
}
WHERE {
  VALUES ?entity {
      <http://acme.test/entity/entity123>
  }
  ?entity ?entityPredicate ?entityObject .
};